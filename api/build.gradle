plugins {
    id 'java-library'
}

repositories {
    mavenLocal()
}

dependencies {
    compileOnly "net.kyori:adventure-text-serializer-plain:4.18.0"
    compileOnly "net.kyori:adventure-text-serializer-legacy:4.18.0"
    compileOnlyApi("org.incendo:cloud-annotations:2.0.0")
    compileOnlyApi("org.incendo:cloud-core:2.0.0")
    compileOnlyApi("net.kyori:adventure-text-minimessage:4.18.0")
    implementation("com.github.ChromMob:ConfigManager:master-SNAPSHOT")
    implementation("com.google.code.gson:gson:2.13.2")
}

tasks.jar {
    enabled = false
}

shadowJar {
    archiveClassifier = ""
    relocate "me.chrommob.config", "me.chrommob.minestore.libs.me.chrommob.config.ConfigManager"
    relocate 'com.google.gson', 'me.chrommob.minestore.libs.com.google.gson'
    minimize {
        exclude(dependency('com.github.ChromMob:ConfigManager:.*'))
        exclude(dependency('com.google.code.gson:gson:.*'))
    }
}

def templateSource = file('src/main/templates')
def templateDest = layout.buildDirectory.dir('generated/sources/templates')

def generateTemplates = tasks.register('generateTemplates', Copy) { task ->
    def props = [
            'version': project.version,
    ]
    task.inputs.properties props

    task.from templateSource
    task.into templateDest
    task.expand props
}

compileJava.dependsOn generateTemplates

sourceSets.main.java.srcDir(generateTemplates.map { it.outputs })